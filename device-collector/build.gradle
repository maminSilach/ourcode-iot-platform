import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
	id 'java'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
	id 'com.github.davidmc24.gradle.plugin.avro' version "${avroPluginVersion}"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'device-collector'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()

	maven {
		url = "https://packages.confluent.io/maven/"
	}
}

dependencies {

	// Actuator
	implementation "org.springframework.boot:spring-boot-starter-actuator"

	// JDBC
	implementation "org.springframework.boot:spring-boot-starter-jdbc"

	// Web
	implementation "org.springframework.boot:spring-boot-starter-web"

	// Kafka
	implementation "org.springframework.kafka:spring-kafka"
	implementation "io.confluent:kafka-avro-serializer:${avroSerializerVersion}"


	// Lombok
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'

	// MapStruct
	implementation "org.mapstruct:mapstruct:${mapStructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

	// Micrometer
	runtimeOnly "io.micrometer:micrometer-registry-prometheus"
	implementation "io.micrometer:micrometer-core"

	// Flyway
	implementation "org.flywaydb:flyway-core:${flywayVersion}"

	// Sharding Sphere
	implementation "org.apache.shardingsphere:shardingsphere-jdbc:${apacheShardingSphereVersion}"

	// Jaxb
	implementation "javax.xml.bind:jaxb-api:${jaxbVersion}"
	runtimeOnly "org.glassfish.jaxb:jaxb-runtime:${jaxbVersion}"

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:kafka'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

generateAvroJava {
	source = file("src/main/resources/avro")
}

compileJava {
	options.compilerArgs += [
			'-Amapstruct.defaultComponentModel=spring'
	]

	dependsOn(generateAvroJava)
}

test {
	useJUnitPlatform()
	testLogging {
		events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
		showStandardStreams = true
		showStackTraces = true
		exceptionFormat = TestExceptionFormat.FULL
	}
}