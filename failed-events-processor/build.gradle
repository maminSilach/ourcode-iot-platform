import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'com.github.davidmc24.gradle.plugin.avro' version "${avroPluginVersion}"

}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'failed-events-processor'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()

    maven {
        url = "https://packages.confluent.io/maven/"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation "io.confluent:kafka-avro-serializer:${avroSerializerVersion}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MapStruct
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

    // Micrometer
    runtimeOnly "io.micrometer:micrometer-registry-prometheus"
    implementation "io.micrometer:micrometer-core"

    // Spring Retry
    implementation "org.springframework.retry:spring-retry:${springRetryVersion}"
    implementation "org.springframework:spring-aspects:${springAspectVersion}"

    // AWS S3
    implementation platform("software.amazon.awssdk:bom:${awsVersion}")
    implementation 'software.amazon.awssdk:s3'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:kafka'
    testImplementation "org.testcontainers:minio:${testContainerMinioVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

generateAvroJava {
    source = file("src/main/resources/avro")
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring'
    ]

    dependsOn(generateAvroJava)
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
        showStandardStreams = true
        showStackTraces = true
        exceptionFormat = TestExceptionFormat.FULL
    }
}
