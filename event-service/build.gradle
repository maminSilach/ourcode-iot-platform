import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
	id 'java'
	id 'org.springframework.boot' version "$springBootVersion"
	id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
	id 'org.openapi.generator' version "$openApiGeneratorPluginVersion"
	id 'maven-publish'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'event-service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

publishing {
	repositories {
		maven {
			name = 'nexus'
			url = "${nexusRepository}"
			allowInsecureProtocol = true

			credentials {
				username = "${nexusUsername}"
				password = "${nexusPassword}"
			}
		}
	}

	publications {
		maven(MavenPublication) {
			artifact("build/libs/event-service-api-" + "${apiVersion}" + ".jar") {
				extension 'jar'
			}

			groupId = 'com.example'
			artifactId = 'event-service-api'
			version = "${apiVersion}"
		}
	}
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = new File("$rootDir/openapi/event-service.yaml").toURI().toString()
	outputDir = layout.buildDirectory.dir('generated').get().asFile.absolutePath
	apiPackage = "com.example.eventservice.api"
	modelPackage = "com.example.eventservice.model"
	invokerPackage = "com.example.eventservice.invoker"
	configOptions = [library        : "spring-cloud",
					 useFeignClients: "true",
					 interfaceOnly  : "true",
					 useJakartaEe   : "true",
					 returnResponseEntity: "false",
					 useResponseEntity: "false",
					 useTags: "true"]
}

tasks.register('annotateFeignClients') {
	dependsOn 'openApiGenerate'
	doLast {
		def apiDir = layout.buildDirectory.dir('generated/src/main/java/com/example/eventservice/api').get().asFile
		apiDir.eachFile { file ->
			if (file.name.endsWith("Api.java")) {
				def text = file.text

				if (!text.contains("import org.springframework.cloud.openfeign.FeignClient;")) {
					text = text.replaceFirst(/(package [^;]+;\s*)/,
							"\$1import org.springframework.cloud.openfeign.FeignClient;\n")
				}

				if (!text.contains("@FeignClient")) {
					text = text.replaceFirst("public interface",
							"@FeignClient(name = \"event-service-api\", url = \"\\\${event.service.base-uri}\")\npublic interface")
				}
				file.write(text)
			}
		}
	}
}

tasks.register('compileGeneratedJava', JavaCompile) {
	dependsOn 'annotateFeignClients'
	source = layout.buildDirectory.dir("generated/src/main/java").get().asFileTree
	destinationDirectory = layout.buildDirectory.dir("classes/generated/main").get().asFile
	classpath = sourceSets.main.compileClasspath
}

tasks.register('generatedClientJar', Jar) {
	dependsOn 'compileGeneratedJava'
	from layout.buildDirectory.dir("classes/generated/main")
	archiveBaseName = 'event-service-api'
	archiveVersion = "${apiVersion}"
}


publish.dependsOn 'generatedClientJar'
publishMavenPublicationToNexusRepository.dependsOn 'generatedClientJar'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-cassandra'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	annotationProcessor 'org.projectlombok:lombok'

	implementation "org.mapstruct:mapstruct:${mapStructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.3.0'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.38'

	implementation 'jakarta.validation:jakarta.validation-api:3.1.1'

	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:cassandra'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

compileJava {
	options.compilerArgs += [
			'-Amapstruct.defaultComponentModel=spring'
	]
}

test {
	useJUnitPlatform()
	testLogging {
		events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
		showStandardStreams = true
		showStackTraces = true
		exceptionFormat = TestExceptionFormat.FULL
	}
}